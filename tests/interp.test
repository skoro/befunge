package require tcltest

source ../shared.tcl
source ../interp.tcl
source ../stack.tcl
source ../code_matrix.tcl
source ../program_counter.tcl
source ../iohandler.tcl

namespace import ::tcltest::test
namespace import ::oo::class
namespace import ::befunge::Interp
namespace import ::befunge::Stack
namespace import ::befunge::CodeMatrix
namespace import ::befunge::ProgramCounter
namespace import ::befunge::IOHandler

proc createInterp {} {
    set i [Interp new        \
        [Stack new]          \
        [CodeMatrix new]     \
        [ProgramCounter new] \
        [IOHandler new]      \
    ]
    $i init
    return $i
}

class create TestIO {
    superclass IOHandler

    variable Out In

    constructor {} { set Out "" }
    method output value { set Out $value }
    method input_int {} { return $In }
    method input_char {} { return $In }
    method get_output {} { return $Out }
    method set_input value { set In $value }
}

test interp-1.1 { checking the states for "@" operation } {
    set i [createInterp]
    [$i code] set_op 0 0 @
    lappend states [$i isRunning] [$i isStopped]
    $i start
    lappend states [$i isRunning] [$i isStopped]
    $i step
    lappend states [$i isRunning] [$i isStopped]
    return $states
} {0 0 1 0 0 1}

test interp-1.2 { checking logical not "!" operation } {
    set i [createInterp]
    [$i code] set_op 0 0 3
    [$i code] set_op 1 0 !
    [$i code] set_op 2 0 0
    [$i code] set_op 3 0 !
    $i start
    times 4 {$i step}
    [$i stack] values
} {0 1}

test interp-1.3 { step over "#" operation } {
    set i [createInterp]
    [$i code] set_op 0 0 "#"
    [$i code] set_op 1 0 5
    [$i code] set_op 2 0 2
    $i start
    $i step
    $i step
    return [list [[$i pc] xy] [[$i stack] size] [[$i stack] pop]]
} {{3 0} 1 2}

test interp-1.4 { greater than "`" operation } {
    set i [createInterp]
    [$i code] set_op 0 0 3
    [$i code] set_op 1 0 2
    [$i code] set_op 2 0 `
    $i start
    times 3 { $i step }
    [$i stack] pop
} 1

test interp-direction-1.1 { the default direction of PC must be right } {
    set i [createInterp]
    $i start
    lappend pos [[$i pc] xy]
    $i step
    lappend pos [[$i pc] xy]
    $i step
    lappend pos [[$i pc] xy]
} {{0 0} {1 0} {2 0}}

test interp-direction-1.2 { checking ">" direction op } {
    set i [createInterp]
    [$i code] set_op 0 0 v
    [$i code] set_op 0 1 >
    $i start
    times 3 { $i step }
    [$i pc] xy
} {2 1}

test interp-direction-1.3 { checking "<" direction op } {
    set i [createInterp]
    [$i code] set_op 2 0 <
    $i start
    times 3 { $i step }
    [$i pc] xy
} {1 0}

test interp-direction-1.4 { checking "v" direction op } {
    set i [createInterp]
    [$i code] set_op 1 0 v
    $i start
    times 3 { $i step }
    [$i pc] xy
} {1 2}

test interp-direction-1.5 { checking "^" direction op } {
    set i [createInterp]
    [$i code] set_op 0 0 ^
    $i start
    $i step
    [$i pc] xy
} {0 24}

test interp-direction-1.6 { horizontal direction if a value is not 0 on the stack } {
    set i [createInterp]
    [$i code] set_op 0 0 9
    [$i code] set_op 1 0 _
    $i start
    $i step
    $i step
    [$i pc] xy
} {0 0}

test interp-direction-1.7 { horizontal direction if a value is 0 on the stack } {
    set i [createInterp]
    [$i code] set_op 0 0 0
    [$i code] set_op 1 0 _
    $i start
    $i step
    $i step
    [$i pc] xy
} {2 0}

test interp-direction-1.8 { vertical direction if a value is not 0 on the stack } {
    set i [createInterp]
    [$i code] set_op 0 0 v
    [$i code] set_op 0 1 9
    [$i code] set_op 0 2 |
    $i start
    times 3 { $i step }
    [$i pc] xy
} {0 1}

test interp-direction-1.9 { vertical direction if a value is 0 on the stack } {
    set i [createInterp]
    [$i code] set_op 0 0 v
    [$i code] set_op 0 1 0
    [$i code] set_op 0 2 |
    $i start
    times 3 { $i step }
    [$i pc] xy
} {0 3}

test interp-direction-1.10 { popup from the right edge to left one } {
    set i [createInterp]
    $i start
    times 82 { $i step }
    [$i pc] xy
} {2 0}

test interp-direction-1.11 { popup from the left edge to right one } {
    set i [createInterp]
    [$i code] set_op 0 0 <
    $i start
    times 82 { $i step }
    [$i pc] xy
} {78 0}

test interp-direction-1.12 { popup from the bottom to up } {
    set i [createInterp]
    [$i code] set_op 0 0 v
    $i start
    times 26 { $i step }
    [$i pc] xy
} {0 1}

test interp-direction-1.13 { popup from the up to bottom } {
    set i [createInterp]
    [$i code] set_op 0 0 ^
    $i start
    times 26 { $i step }
    [$i pc] xy
} {0 24}

test interp-stack-1.1 { pushing numeric values onto stack } {
    set i [createInterp]
    [$i code] set_op 0 0 8
    [$i code] set_op 1 0 2
    [$i code] set_op 2 0 6
    $i start
    times 3 {$i step}
    return [list [[$i stack] pop] [[$i stack] pop] [[$i stack] pop]]
} {6 2 8}

test interp-stack-1.2 { dup the stack value } {
    set i [createInterp]
    [$i code] set_op 0 0 5
    [$i code] set_op 1 0 :
    $i start
    $i step
    $i step
    [$i stack] lpop 2
} {5 5}

test interp-stack-1.3 { swap the stack value } {
    set i [createInterp]
    [$i code] set_op 0 0 5
    [$i code] set_op 1 0 2
    [$i code] set_op 2 0 "\\"
    $i start
    times 3 { $i step }
    [$i stack] lpop 2
} {5 2}

test interp-stack-1.4 { pop the stack value } {
    set i [createInterp]
    [$i code] set_op 0 0 6
    [$i code] set_op 1 0 9
    [$i code] set_op 2 0 "\$"
    $i start
    times 3 { $i step }
    [$i stack] top
} 6

test interp-math-1.1 { checking + math operation } {
    set i [createInterp]
    [$i code] set_op 0 0 3
    [$i code] set_op 1 0 2
    [$i code] set_op 2 0 +
    $i start
    times 3 { $i step }
    [$i stack] pop
} 5

test interp-math-1.2 { checking - math operation } {
    set i [createInterp]
    [$i code] set_op 0 0 3
    [$i code] set_op 1 0 2
    [$i code] set_op 2 0 -
    $i start
    times 3 { $i step }
    [$i stack] pop
} 1

test interp-math-1.3 { checking * math operation } {
    set i [createInterp]
    [$i code] set_op 0 0 2
    [$i code] set_op 1 0 3
    [$i code] set_op 2 0 *
    $i start
    times 3 { $i step }
    [$i stack] pop
} 6

test interp-math-1.4 { checking / math operation } {
    set i [createInterp]
    [$i code] set_op 0 0 8
    [$i code] set_op 1 0 4
    [$i code] set_op 2 0 /
    $i start
    times 3 {$i step}
    [$i stack] pop
} 2

test interp-math-1.5 { checking % math operation } {
    set i [createInterp]
    [$i code] set_op 0 0 3
    [$i code] set_op 1 0 2
    [$i code] set_op 2 0 %
    $i start
    times 3 {$i step}
    [$i stack] pop
} 1

test interp-stringmode-1.1 { enabling leaves character codes onto the stack } {
    set i [createInterp]
    [$i code] set_op 0 0 \"
    [$i code] set_op 1 0 "a"
    [$i code] set_op 2 0 "b"
    [$i code] set_op 3 0 \"
    $i start
    times 4 { $i step }
    [$i stack] values
} {97 98}

test interp-stringmode-1.2 { only ascii characters are allowed } -body {
    set i [createInterp]
    [$i code] set_op 0 0 "\""
    [$i code] set_op 1 0 "Ñˆ"
    $i start
    times 2 { $i step }
} -returnCodes error -result "Only ascii characters are allowed"

test interp-io-1.1 { operation "&" will ask for a number } {
    set io [TestIO new]
    $io set_input 6
    set i [Interp new [Stack new] [CodeMatrix new] [ProgramCounter new] $io]
    [$i code] set_op 0 0 &
    $i start
    $i step
    [$i stack] top
} 6

test interp-io-1.2 { operation "~" will ask for a char } {
    set io [TestIO new]
    $io set_input "A"
    set i [Interp new [Stack new] [CodeMatrix new] [ProgramCounter new] $io]
    [$i code] set_op 0 0 "~"
    $i start
    $i step
    [$i stack] top
} 65

test interp-io-1.3 { operation "." outputs the value as an integer } {
    set io [TestIO new]
    set i [Interp new [Stack new] [CodeMatrix new] [ProgramCounter new] $io]
    [$i code] set_op 0 0 \"
    [$i code] set_op 1 0 "A"
    [$i code] set_op 2 0 \"
    [$i code] set_op 3 0 "."
    $i start
    times 4 { $i step }
    $io get_output
} 65

test interp-io-1.4 { operation "," outputs the value as a character } {
    set io [TestIO new]
    set i [Interp new [Stack new] [CodeMatrix new] [ProgramCounter new] $io]
    [$i code] set_op 0 0 \"
    [$i code] set_op 1 0 "A"
    [$i code] set_op 2 0 \"
    [$i code] set_op 3 0 ","
    $i start
    times 4 { $i step }
    $io get_output
} "A"

test interp-put-1.1 { checking "p" (put) operation } {
    set i [createInterp]
    [$i code] set_op 0 0 9
    [$i code] set_op 1 0 4
    [$i code] set_op 2 0 5
    [$i code] set_op 3 0 "p"
    $i start
    times 4 { $i step }
    [$i code] get 4 5
} 9

test interp-get-1.1 { checking "g" (get) operation } {
    set i [createInterp]
    [$i code] set_op 4 5 8
    [$i code] set_op 0 0 4
    [$i code] set_op 1 0 5
    [$i code] set_op 2 0 "g"
    $i start
    times 4 { $i step }
    [$i stack] top
} 8

::tcltest::cleanupTests
